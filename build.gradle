import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4' apply false // version 관리
    id 'io.spring.dependency-management' version '1.1.7' apply false // version 관리
}

allprojects {
    group = 'com.laze.framework'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects { // frontend 제외하고 적용하려면 조건을 추가할 수 있음 (if project.name != 'frontend')
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    // dependencyManagement: 모든 Java 하위 프로젝트에 Spring Boot BOM 적용
    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
        // 공통 라이브러리 버전 관리
        dependencies {
            dependency 'org.mapstruct:mapstruct:1.6.3'
        }
    }

    // dependencies: 모든 Java 하위 프로젝트에 공통으로 필요한 의존성
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        implementation 'org.mapstruct:mapstruct:1.6.3'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    }

    test {
        useJUnitPlatform()
    }
}
